import groovy.json.JsonSlurper

def factorioInstallationFolder = 'C:\\Factorio_standalone'

subprojects {
    apply plugin: 'base'

    task zip(type: Zip) {
        def info = new JsonSlurper().parse(file('info.json'))
        def name = "${info.name}_${info.version}"
        archiveFileName.set("${name}.zip")
        destinationDirectory = file('build')
        into name

        from ('./src') {
            include '**'
        }

        from ('.') {
            include 'info.json'
            include 'README.md'
            include 'changelog.txt'
        }
    }
    assemble.dependsOn(zip)

    task deploy(type: Copy) {
        from "$buildDir"
        into "$factorioInstallationFolder\\mods"
    }
    deploy.dependsOn(build)

    task modJson() {
        doLast {
            def info = new JsonSlurper().parse(file('info.json'))
            def file = file("$buildDir/mod-list.json")
            file.text = """{
  "mods": [
    { "name": "base", "enabled": true },
    { "name": "creative-mod", "enabled": true },
    { "name": "SetGameSpeed", "enabled": true },
    { "name": "${info.name}", "enabled": true }
  ]
}"""
        }
    }
    modJson.dependsOn(build)
}
