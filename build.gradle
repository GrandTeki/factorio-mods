import groovy.json.JsonSlurper

def modsFolder = 'C:\\Factorio_standalone\\mods'

subprojects {
    apply plugin: 'base'

    task zip(type: Zip) {
        def info = new JsonSlurper().parse(file('info.json'))
        def name = "${info.name}_${info.version}"
        archiveFileName.set("${name}.zip")
        destinationDirectory = file('build')
        into name

        from ('./src') {
            include '**'
        }

        from ('.') {
            include 'info.json'
            include 'README.md'
            include 'changelog.txt'
        }
    }
    assemble.dependsOn(zip)

    task deploy(type: Copy) {
        from buildDir
        into modsFolder
        dependsOn(build)
    }

    task cleanDeploy(type: Delete) {
        doFirst {
            def info = new JsonSlurper().parse(file('info.json'))
            delete fileTree(modsFolder)
                .filter { it.isFile() && it.name.startsWith(info.name) }
                .files
        }
    }

    task modJson() {
        doFirst {
            def info = new JsonSlurper().parse(file('info.json'))
            def file = file("$buildDir/mod-list.json")
            file.text = """{
  "mods": [
    { "name": "base", "enabled": true },
    { "name": "creative-mod", "enabled": true },
    { "name": "SetGameSpeed", "enabled": true },
    { "name": "${info.name}", "enabled": true }
  ]
}"""
        }
        dependsOn(cleanDeploy)
    }
    modJson.dependsOn(build)
}
